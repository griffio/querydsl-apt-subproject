buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "com.ewerk.gradle.plugins:querydsl-plugin:${querydsl_plugin_version}"
  }
}

subprojects {

  apply plugin: 'java'

  apply plugin: 'ivy-publish'

  group = 'griffio'
  version = '0.1.0'

  repositories {
    jcenter()
    ivy {
      url "${rootDir}/repo"
    }
  }

  publishing {
    publications {
      ivyJava(IvyPublication) {
        from components.java
      }
    }
    repositories {
      ivy {
        url "${rootDir}/repo"
      }
    }
  }

  dependencies {

    compile("com.google.guava:guava:${guava_version}",
        "com.mysema.querydsl:querydsl-collections:${querydsl_version}",
        "com.mysema.querydsl:querydsl-jpa:${querydsl_version}",
        "org.eclipse.persistence:javax.persistence:${jpa_version}",
        "com.google.code.findbugs:jsr305:${jsr305_version}",
        "org.slf4j:slf4j-api:${slf4j_api_version}",
        "ch.qos.logback:logback-classic:${logback_version}"
    )

    testCompile("com.google.guava:guava-testlib:$guava_version",
        "junit:junit:${junit_version}",
        "com.google.truth:truth:${truth_version}")
  }

  tasks.withType(JavaCompile) {
    description = "all javac tasks: ignore processor hints, keep all other hints"
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
    options.compilerArgs += ['-Xlint:all,-processing']
    options.encoding = "UTF-8"
  }

  task wrapper(type: Wrapper) {
    gradleVersion = 2.3
  }

  test {
    useTestNG()
    maxHeapSize = 128
    minHeapSize = 64
  }

}

project(':parts') {

  dependencies {
    compile "griffio:system:0.1.0"
  }

}